---
title: Shell脚本笔记整理(六) awk命令
subtitle: 
date: 2019-03-24
tags: ["运维", "shell"]
draft: false
---

本章记录shell三剑客最后一个：awk

<!--more-->

## 语法格式

```
# BEGIN:        处理前执行代码块
# pattern:      匹配模式，不写则对每行进行处理
# commands:     处理指令
# END:          处理后执行代码块
# file_name:    需处理文件源

# 第一种

awk 'BEGIN{}pattern{commands}END{}' file_name

# 第二种

stdout | awk 'BEGIN{}pattern{commands}END{}'
```

## 内置变量

在处理文本流时，awk预先定义了变量用以表示处理的文本。可以通过定义或者读取变量达到不同目的。

|内置变量   |含义|
|----------|----|
|$0         |打印整行内容|
|$1-$n      |当前行的第1-n个字段，默认以空格为分隔符|
|NF         | (Number Field) 当前行的字段个数，也就是有多少列|
|NR         | (Number Row) 当前行的行号，从1开始计数|
|FNR        | (File Number Row) 多文件处理时，每个文件行号单独计数，都是从0开始|
|FS         | (Field Separator) 输入字段分隔符，不指定默认以空格或tab键分割|
|RS         | (Row Separator) 输入行分隔符，默认回车换行|
|OFS        | (Output Field Separator) 输出字段分隔符，默认为空格|
|ORS        | (Output Row Separator) 输出行分隔符，默认为回车换行|
|FILENAME   |当前输入的文件名字|
|ARGC       |命令行参数个数|
|ARGV       |命令行参数数组|

## printf

类似C语言的printf，可以进行格式化输出。当然回车默认也是没有的，需要显式带上。

|格式符|含义|
|---|---|
|%s|打印字符巛|
|%d|打印十进制数|
|%f|打印一个浮点数|
|%x|打印十六进制数|
|%o|打印八进制数|
|%e|打印数字的科学计数法形式|
|%c|打印单个字符的ASCII码|

## 匹配模式

匹配模式可选，如果没有就处理每一行。

支持两种匹配规则，正则表达式和关系运算。

### RegExp

例:

1. 匹配/etc/passwd文件中含有root字符串的所有行
```shell
# 以":"分隔字段，匹配root，$0输出整行
awk 'BEGIN{FS=":"}/root/{print $0}' /etc/passwd
```

2. 匹配/etc/passed文件行中以yarn开头的所有行
```shell
awk 'BEGIN{FS=":"}/^yarn/{print $0}' /etc/passwd
```

### 关系运算

|符号|含义|
|---|---|
|<|小于|
|>|大于|
|<=|小于等于|
|>=|大于等于|
|==|等于|
|!=|不等于|
|~|匹配正则表达式|
|!~|不匹配正则表达式|
|\|\||或|
|&&|与|
|!|非|

例:

以:为分隔符，匹配/etc/passwd文件中第3个字段小于50的所有行信息

```
awk 'BEGIN{FS=":"}$3<50{print $0}' /etc/passwd
```

以:为分隔符，匹配/etc/passwd文件中第7个字段不为/bin/bash的所有行信息

```
awk 'BEGIN{FS=":"}$7!="/bin/bash"{print $0}' /etc/passwd
```

以:为分隔符，匹配/etc/passwd文件中第3个字段包含3个以上数字的所有行

```
awk 'BEGIN{FS=":"}$3~/[0-9]{3,}/{print $0}' /etc/passwd
```

以:为分隔符，匹配/etc/passwd文件中以hdfs或yarn的开头的所有行信息

```
awk 'BEGIN{FS=":"}$1=="hdfs" || $1=="yarn"{print $0}' /etc/passwd
```

以:为分隔符，匹配/etc/passwd文件中第3个字段小于50并且第4个字段大于50的所有行信息

```
awk 'BEGIN{FS=":"}$3<50 && $4>50{print $0}' /etc/passwd
```

## 算数运算

|运算符|含义|
|---|---|
|+|加|
|-|减|
|*|乘|
|/|除|
|%|模|
|^或**|乘方|
|++x|在返回x变量之前，x变量加1|
|x++|在返回x变量之后，x变量加1|

类似其他编程语言

例:
```shell
awk 'BEGIN{num1=20;num2=30;print num1+num2}'
Output: 50

awk 'BEGIN{num1=20;num2=num1++;print num1,num2}'
Output: 20 21
```

稍微复杂的例子，计算学生平均分，已知有文件student.txt

```shell
# student.txt
Allen   80  90  96  98
Mike    93  98  92  91
Zhang   78  76  87  92
Jerry   86  89  68  92
Han     85  95  75  90
Li      78  88  98  100
```

计算平均分的语句如下

```shell
# 其中%-5d是C语言中占位符写法，意思为向左对齐（-）占用5格（5）的数字（d）。
awk '{total=$2+$3+$4+$5;AVG=total/4;printf "%-8s%-5d%-5d%-5d%-5d%0.2f\n",$1,$2,$3,$4,$5,AVG}' student.txt

Output:
Allen   80   90   96   98   91.00
Mike    93   98   92   91   93.50
Zhang   78   76   87   92   83.25
Jerry   86   89   68   92   83.75
Han     85   95   75   90   86.25
Li      78   88   98   100  91.00
```

接下来给输出的表加上表头

```shell
awk 'BEGIN{printf "%-8s%-8s%-8s%-8s%-8s%s\n","Name","Chinese","English","Math","Physics", "AVG"}{total=$2+$3+$4+$5;AVG=total/4;printf "%-8s%-8d%-8d%-8d%-8d%0.2f\n",$1,$2,$3,$4,$5,AVG}' student.txt

Output:
Name    Chinese English Math    Physics AVG
Allen   80      90      96      98      91.00
Mike    93      98      92      91      93.50
Zhang   78      76      87      92      83.25
Jerry   86      89      68      92      83.75
Han     85      95      75      90      86.25
Li      78      88      98      100     91.00
```

## 条件语句和循环语句

awk中可以使用常规编程中的if、if-else、if-else-if条件语句，和while、do-while、for循环语句。

例: 以":"为分隔符，只打印/etc/passwd中第3个字段的数字在50-100范围内的行信息

```
awk 'BEGIN{FS=":"}{if($3>50 && $3<100) print $0}' /etc/passwd
```

看到上面的例子，应该清楚编写复杂的awk语句可读性非常低。因此awk支持将语句单独写入一个文件中。

### 以":"为分隔符，详细打印/etc/passwd中第3个字段的数字在50-100范围内的行信息

```shell
# script.awk
BEGIN{
    FS=":"
}

{
    if ($3<50)
    {
        printf "%-20s%-25s%-5d\n", "小于50的UID",$1,$3
    }
    else if($3>50 && $3<100)
    {
        printf "%-20s%-25s%-5d\n", "大于50小于100的UID",$1,$3
    }
    else
    {
        printf "%-20s%-25s%-5d\n", "大于100的UID",$1,$3
    }
}

# 如果要使用script.awk文件，使用-f参数指定语句文件路径，后面再接要处理的文件路径。
awk -f script.awk /etc/passwd
```

### 计算下列每个同学的平均分数，并且只打印平均分数大于90的同学姓名和分数信息

```shell
# student.txt
Allen   80   90   96   98   91.00
Mike    93   98   92   91   93.50
Zhang   78   76   87   92   83.25
Jerry   86   89   68   92   83.75
Han     85   95   75   90   86.25
Li      78   88   98   100  91.00

# student.awk
BEGIN{
    printf "%-10s%-10s%-10s%-10s%-10s%-10s\n","Name","Chinese","English","Math","Physical","Average"
}

{
    total=$2+$3+$4+$5
    avg=total/4
    if(avg>90)
    {
        printf "%-10s%-10s%-10s%-10s%-10s%0.2f\n",$1,$2,$3,$4,$5,avg
        score_chinese+=$2
        score_english+=$3
        score_math+=$4
        score_physical+=$5
    }
}

END{
    printf "%-10s%-10d%-10d%-10d%-10d\n","",score_chinese,score_english,score_math,score_physical
}
```

### 计算1+2+3+4+...+100的和

这里分别使用while、do-while、for三种循环方式实现

while:

```shell
BEGIN{
    # i不显式声明这默认为0
    while(i<=100)
    {
        sum+=i
        i++
    }
    print sum
}
```

do-while:

```shell
BEGIN{
    do
    {
        sum+=i
        i++
    }while(i<=100)
    print sum
}
```

for:

```shell
BEGIN{
    for(i=0;i<=100;i++)
    {
        sum+=i
    }
    print sum
}
```

从上面的例子可以看出，语法非常接近C语言。

## 字符串函数

|函数名|解释|函数返回值|
|---|---|---|
|length(str)|计算字符串长度|整数长度值|
|index(str1,str2)|在str1中查找str2的位置|返回值为位置索引，从1计数|
|tolower(str)|转换为小写|转换后的小写字符串|
|toupper(str)|转换为大写|转换后的大写字符串|
|substr(str,m,n)|从str的m个字符开始，截取n位|截取后的字串|
|split(str,arr,fs)|按fs切割字符串，结果保存arr|切割后的字串的个数|
|match(str,RE)|在str中按照RE查找，返回位置|返回索引位置|
|sub(RE,RepStr,str)|在str中搜索符合RE的字串，将其替换为RepStr；只替换第一个|替换的个数|
|gsub(RE,RepStr,str)|在str中搜索符合RE的字串，将其替换为RepStr；替换所有|替换的个数|

## 常用选项

|选项|解释|
|---|---|
|-v|参数传递|
|-f|指定脚本文件|
|-F|指定分隔符|
|-V|查看awk的版本号|

-v可以传递外部变量到awk处理中，例:

```shell
var1="hello world"
num1=20

# 因为var1变量带空格，使用时必须用双引号括起来
awk -v var="$var1" -v num=$num1 'BEGIN{print num,var}'

Output:
20 hello world
```

-f上面提到过，不作赘述。

-F指定字段分隔符，例:

```shell
awk -F ':' '{print $7}' /etc/passwd

# 该语句与以下等价

awk 'BEGIN{FS=":"}{print $7}' /etc/passwd
```

## 数组

先说下Shell中数组的用法。

定义:

```
array=("Allen","Mike","Messi","Jerry","Hanmeimei","Wang"...)
```

|操作|示例|
|---|---|
|打印元素|echo ${array[2]}|
|打印元素个数|echo ${#array[@]}|
|打印元素长度|echo ${#array[3]}|
|给元素赋值|array[3]="Li"|
|删除元素|unset array[2];unset array|
|分片访问|echo ${array[@]:1:3}|
|元素内容替换|${array[@]/e/E}只替换第一个e<br/>${array[@]//e/E}替换所有的e|
|数组的遍历|for a in ${array[@]}<br/>do<br/>&emsp;echo $a<br/>done|

awk中的数组更像其他语言的map，索引不光可以使数字，也可以是字符串。

定义:

```
array_name[index]=value
```

创建一个数组遍历一次，之后删除一个元素

```shell
awk 'BEGIN{
    # 索引为0的是数字，其余的索引为字符串
    # 如果在命令行中写成一行，每项赋值需用";"号隔开
    arr[0]="hello"
    arr["one"]="world"
    arr["two"]="!"

    # str为arr的下标
    for (str in arr)
    {
        print arr[str]
    }

    # 删除数组元素
    delete arr["one"]

    # 再次遍历输出，输出一个空行作区分
    print ""
    for (str in arr)
    {
        print arr[str]
    }
}'

Output:
!
world
hello

!
hello
```

awk数组有个典型用法就是统计动态的数据，下面是一个统计学生总分的例子:

```shell
# student.txt
Allen   80   90   96   98
Mike    93   98   92   91
Zhang   78   76   87   92
Jerry   86   89   68   92
Han     85   95   75   90
Li      78   88   98   100

# awk
awk 'BEGIN{
    printf "%-10s|%-5s\n","Name","Total"
    print "----------+-----"
}
{
    # 学生名作index，对该学生总分进行累加
    student_source[$1]+=$2
    student_source[$1]+=$3
    student_source[$1]+=$4
    student_source[$1]+=$5
}
END{
    for (s in student_source)
    {
        printf "%-10s|%-5d\n",s,student_source[s]
    }
}' student.txt

Output:
Name      |Total
----------+-----
Zhang     |333
Jerry     |335
Mike      |374
Han       |345
Allen     |364
Li        |364
```

这样无论有多少学生都可以统计出所有学生的总分成绩。

### 多维数组

awk数组本身不支持多维数组，但是根据key可以为字符串的特性，组合索引的方式进行存储，下面是网上看到的一种用法:

```shell
awk 'BEGIN{
    array["0,0"]=100
    array["0,1"]=200
    array["0,2"]=300
    array["1,0"]=400
    array["1,1"]=500
    array["1,2"]=600
    # 输出数组元素
    for (arr in array)
    {
        print "array[",arr,"] = ",array[arr]
    }
}'

Output:
array[ 1,1 ] =  500
array[ 1,2 ] =  600
array[ 0,0 ] =  100
array[ 0,1 ] =  200
array[ 0,2 ] =  300
array[ 1,0 ] =  400
```
